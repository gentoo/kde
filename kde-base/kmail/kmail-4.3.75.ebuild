# Copyright 1999-2009 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: $

EAPI="2"

KMNAME="kdepim"
inherit kde4-meta

DESCRIPTION="KMail is the email component of Kontact, the integrated personal information manager of KDE."
KEYWORDS="~alpha ~amd64 ~hppa ~ia64 ~ppc ~ppc64 ~x86"
IUSE="debug +handbook +semantic-desktop"

DEPEND="
	$(add_kdebase_dep kdelibs 'semantic-desktop?')
	$(add_kdebase_dep libkdepim)
	$(add_kdebase_dep libkleo)
	$(add_kdebase_dep libkpgp)
	$(add_kdebase_dep libksieve)
	$(add_kdebase_dep mimelib)
"
RDEPEND="${DEPEND}
	$(add_kdebase_dep kmailcvt)
	semantic-desktop? ( $(add_kdebase_dep nepomuk) )
"
add_blocker messagecore
add_blocker messagelist
add_blocker messageviewer

KMEXTRACTONLY="
	korganizer/org.kde.Korganizer.Calendar.xml
	libkleo
	libkpgp
	libksieve
	mimelib
"
KMEXTRA="
	ksendemail/
	messagecore/
	messagelist/
	plugins/kmail/
"
KMLOADLIBS="libkdepim"

src_configure() {
	mycmakeargs="${mycmakeargs}
		$(cmake-utils_use_with semantic-desktop Nepomuk)"

	kde4-meta_src_configure
}

src_compile() {
	# Bug #276377: kontact/ can build before kmail/, causing a dependency not to be built
	# Upstream as KDE Bug #198807
	# (setting via MAKEOPTS to trigger a repoman warning)
	MAKEOPTS="${MAKEOPTS} -j1"
	kde4-meta_src_compile
}

pkg_postinst() {
	kde4-meta_pkg_postinst

	if ! has_version kde-base/kdepim-kresources:${SLOT}; then
		echo
		elog "For groupware functionality, please install kde-base/kdepim-kresources:${SLOT}"
		echo
	fi
}
