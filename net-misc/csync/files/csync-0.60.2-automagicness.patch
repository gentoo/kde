--- ocsync-0.60.2/cmake/Modules/MacroOptionalFindPackage.cmake
+++ ocsync-0.60.2/cmake/Modules/MacroOptionalFindPackage.cmake
@@ -0,0 +1,47 @@
+# - MACRO_OPTIONAL_FIND_PACKAGE() combines FIND_PACKAGE() with an OPTION()
+# MACRO_OPTIONAL_FIND_PACKAGE( <name> [QUIT] )
+# This macro is a combination of OPTION() and FIND_PACKAGE(), it
+# works like FIND_PACKAGE(), but additionally it automatically creates
+# an option name WITH_<name>, which can be disabled via the cmake GUI.
+# or via -DWITH_<name>=OFF
+# The standard <name>_FOUND variables can be used in the same way
+# as when using the normal FIND_PACKAGE()
+
+# Copyright (c) 2006-2010 Alexander Neundorf, <neundorf@kde.org>
+#
+# Redistribution and use is allowed according to the terms of the BSD license.
+# For details see the accompanying COPYING-CMAKE-SCRIPTS file.
+
+# This is just a helper macro to set a bunch of variables empty.
+# We don't know whether the package uses UPPERCASENAME or CamelCaseName, so we try both:
+macro(_MOFP_SET_EMPTY_IF_DEFINED _name _var)
+   if(DEFINED ${_name}_${_var})
+      set(${_name}_${_var} "")
+   endif(DEFINED ${_name}_${_var})
+
+   string(TOUPPER ${_name} _nameUpper)
+   if(DEFINED ${_nameUpper}_${_var})
+      set(${_nameUpper}_${_var}  "")
+   endif(DEFINED ${_nameUpper}_${_var})
+endmacro(_MOFP_SET_EMPTY_IF_DEFINED _package _var)
+
+
+macro (MACRO_OPTIONAL_FIND_PACKAGE _name )
+   option(WITH_${_name} "Search for ${_name} package" ON)
+   if (WITH_${_name})
+      find_package(${_name} ${ARGN})
+   else (WITH_${_name})
+      string(TOUPPER ${_name} _nameUpper)
+      set(${_name}_FOUND FALSE)
+      set(${_nameUpper}_FOUND FALSE)
+
+      _mofp_set_empty_if_defined(${_name} INCLUDE_DIRS)
+      _mofp_set_empty_if_defined(${_name} INCLUDE_DIR)
+      _mofp_set_empty_if_defined(${_name} INCLUDES)
+      _mofp_set_empty_if_defined(${_name} LIBRARY)
+      _mofp_set_empty_if_defined(${_name} LIBRARIES)
+      _mofp_set_empty_if_defined(${_name} LIBS)
+      _mofp_set_empty_if_defined(${_name} FLAGS)
+      _mofp_set_empty_if_defined(${_name} DEFINITIONS)
+   endif (WITH_${_name})
+endmacro (MACRO_OPTIONAL_FIND_PACKAGE)

--- ocsync-0.60.2/CMakeLists.txt
+++ ocsync-0.60.2/CMakeLists.txt
@@ -38,8 +38,9 @@
 # add macros
 include(MacroAddPlugin)
 include(MacroCopyFile)
+include(MacroOptionalFindPackage)
 
-find_package(Log4C)
+macro_optional_find_package(Log4C)
 find_package(CMocka)
 if (CMOCKA_FOUND AND UNIT_TESTING)
     include(AddCMockaTest)

--- ocsync-0.60.2/DefineOptions.cmake
+++ ocsync-0.60.2/DefineOptions.cmake
@@ -1,4 +1,3 @@
-option(WITH_LOG4C "Build csync without log4c" ON)
 option(UNIT_TESTING "Build with unit tests" OFF)
 option(MEM_NULL_TESTS "Enable NULL memory testing" OFF)
 option(LOG_TO_CALLBACK "Enable extended logging through a callback" OFF)

--- ocsync-0.60.2/doc/CMakeLists.txt
+++ ocsync-0.60.2/doc/CMakeLists.txt
@@ -2,7 +2,10 @@
 # Build the documentation
 #
 
-include(UseDoxygen OPTIONAL)
+option(WITH_APIDOC "Use doxygen to generate documentation" ON)
+if (WITH_APIDOC)
+  include(UseDoxygen OPTIONAL)
+endif (WITH_APIDOC)
 
 file(GLOB _manpages *.[0-9].txt)
 add_custom_target(man

--- ocsync-0.60.2/modules/CMakeLists.txt
+++ ocsync-0.60.2/modules/CMakeLists.txt
@@ -1,8 +1,8 @@
 project(modules C)
 
-find_package(Libsmbclient)
-find_package(LibSSH 0.4.0)
-find_package(Neon)
+macro_optional_find_package(Libsmbclient)
+macro_optional_find_package(LibSSH 0.4.0)
+macro_optional_find_package(Neon)
 
 set(PLUGIN_VERSION_INSTALL_DIR "${PLUGIN_INSTALL_DIR}-${LIBRARY_SOVERSION}")
 
